 #lang eopl
;Program Mukherjee

;scanner
(define scanner-spec-let
  '((white-sp (whitespace) skip)
    (comment ("%" (arbno (not #\newline))) skip)
    (identifier (letter (arbno (or letter digit))) symbol)
    (number ((or "-" "+" digit)(arbno digit)) number)))

(define grammar-let
  '((Program (Exp)
             a-Program)
    (Exp (number)
         const-Exp)
    (Exp ("-" "(" Exp "," Exp ")")
         diff-Exp)
    (Exp ("zero?" "(" Exp ")")
         zero-Exp?)
    (Exp ("if" Exp "then" Exp "else" Exp)
         if-Exp)
    (Exp (identifier)
         var-Exp)
    (Exp ("let" identifier "=" Exp "in" Exp)
         let-Exp)
    (Exp ("proc" "(" identifier ")" Exp)
         proc-Exp)
    (Exp ("(" Exp Exp ")")
         call-Exp)
    (Exp ("letrec" identifier "(" identifier ")" "=" Exp "in" Exp)
         letrec-Exp)))

(sllgen:make-define-datatypes scanner-spec-let grammar-let)

(define list-the-datatypes
  (lambda ()
    (sllgen:list-define-datatypes scanner-spec-let grammar-let)))

(define just-scan
  (sllgen:make-string-scanner scanner-spec-let grammar-let))

(define scan&parse
  (sllgen:make-string-parser scanner-spec-let grammar-let))

(define run
  (lambda (str)
    (value-of-Program (scan&parse str))))
 
;Enviroment Expression
(define-datatype environment environment?
  (empty-env)
  (extend-env (var symbol?)
              (val Expval?)
              (saved-env environment?))
  (extend-env-rec (p-name symbol?)
                  (b-var symbol?)
                  (body Exp?)
                  (env environment?)))

(define apply-env
  (lambda (env search-var)
    (cases environment env
      (empty-env () #f)
      (extend-env (var val saved-env)
                  (if (eqv? search-var var) val
                      (apply-env saved-env search-var)))
      (extend-env-rec (p-name b-var body saved-env)
                      (if (eqv? search-var p-name)
                          (proc-val (procedure b-var body env))
                          (apply-env saved-env search-var))))))                  
                  
(define init-env
  (lambda ()
    (extend-env 'i (num-val 1)
                (extend-env 'v (num-val 5)
                            (extend-env 'x (num-val 10)
                                        (empty-env))))))
;Expressed Values
(define-datatype Expval Expval?
  (num-val (num number?))
  (bool-val (bool boolean?))
   (proc-val (proc proc?)))
  

(define Expval->num
  (lambda (val)
    (cases Expval val
      (num-val (num)
               num)
      (else
       (report-Expval-extractor-error 'num val)))))

(define Expval->bool
  (lambda (val)
    (cases Expval val
      (bool-val (bool)
               bool)
      (else
       (report-Expval-extractor-error 'bool val)))))

(define Expval->proc
  (lambda (val)
    (cases Expval val
      (proc-val (proc)
               proc)
      (else
       (report-Expval-extractor-error 'proc val)))))


(define report-Expval-extractor-error
  (lambda (type val)
    (eopl:error 'Exp-val "Error extracting ~s from ~s" type val)))

;Continuation
(define-datatype continuation continuation?
  (end-cont)
  (zero1-cont (cont continuation?))
  (let-exp-cont (var symbol?)
                (body Exp?)
                (env environment?)
                (cont continuation?))
  (if-test-cont (exp2 Exp?)
                (exp3 Exp?)
                (env environment?)
                (cont continuation?))
  (diff1-cont (exp2 Exp?)
              (env environment?)
              (cont continuation?))
  (diff2-cont (exp1 Expval?)
              (cont continuation?))
  (rator-cont (rand Exp?)
              (env environment?)
              (cont continuation?))
  (rand-cont (val1 Expval?)
             (cont continuation?)))

(define apply-cont
  (lambda (cont val)
    (cases continuation cont
      (end-cont ()
                (begin
                  (eopl:printf "End of Computation.~%")
                  val))
      (zero1-cont (saved-cont)
                  (apply-cont saved-cont
                              (bool-val (zero? (Expval->num val)))))
      (let-exp-cont (var body saved-env saved-cont)
                    (value-of/k body (extend-env var val saved-env) saved-cont))
      (if-test-cont (exp2 exp3 saved-env saved-cont)
                    (if (Expval->bool val)
                        (value-of/k exp2 saved-env saved-cont)
                        (value-of/k exp3 saved-env saved-cont)))
      (diff1-cont (exp2 saved-env saved-cont)
                  (value-of/k  exp2 saved-env (diff2-cont val saved-cont)))
      (diff2-cont (val1 saved-cont)
                  (let ((num1 (Expval->num val1))
                        (num2 (Expval->num val)))
                    (apply-cont saved-cont
                                (num-val (- num1 num2)))))
      (rator-cont (rand env cont)
                  (value-of/k rand env
                              (rand-cont val cont)))
      (rand-cont (val1 cont)
                 (let ((proc1 (Expval->proc val1)))
                   (apply-procedure/k proc1 val cont))))))

;Value-of Continuation
(define value-of-Program
  (lambda (pgm)
    (cases Program pgm
      (a-Program (Exp)
                 (value-of/k Exp (init-env) (end-cont))))))

(define value-of/k
  (lambda (exp env cont)
    (cases Exp exp
      (const-Exp (num)
                 (apply-cont cont (num-val num)))
      (var-Exp (var)
               (apply-cont cont (apply-env env var)))
      (proc-Exp (var body)
                (apply-cont cont
                            (proc-val (procedure var body env))))
      (letrec-Exp (p-name b-var p-body letrec-body)
                  (value-of/k letrec-body
                              (extend-env-rec p-name b-var p-body env) cont))
      (zero-Exp? (exp1)
                 (value-of/k exp1 env (zero1-cont cont)))
      (if-Exp (exp1 exp2 exp3)
              (value-of/k exp1 env
                          (if-test-cont exp2 exp3 env cont)))
      (let-Exp (var exp1 body)
               (value-of/k  exp1 env
                            (let-exp-cont var body env cont)))
      (diff-Exp (exp1 exp2)
                (value-of/k exp1 env
                            (diff1-cont exp2 env cont)))
      (call-Exp (rator rand)
                (value-of/k rator env
                            (rator-cont rand env cont))))))

;Procedure Datatype
(define-datatype proc proc?  
  (procedure (var symbol?)
             (body Exp?)
             (saved-env environment?)))

(define apply-procedure/k
  (lambda (proc1 val cont)
    (cases proc proc1                     
      (procedure (var body saved-env)
                 (value-of/k body (extend-env var val saved-env) cont )))))      


(define report-error
  (lambda ()
    (eopl:error 'Exp "Error")))


;SUDO TEST CASES
(define test
  "let x = 5 in x")

(define test_1
  "letrec double (x)
                  = if zero?(x)
                    then 0
                    else -(( double - (x,1)), -2)
                    in (double 6)")

(define test_2_
  "let f = proc (y) -(y, 3) in (f 10)")

(define test_3
  "let x = 10 in - (x, 3)")

(define test_4
  "let x = 200
   in let f = proc (z) -(z,x)
      in let x = 100
         in let g = proc (z) -(z,x)
            in -((f 1) , (g 1))")

(define multiplier
  "let makemult = proc (maker)
                  proc (x)
                  if zero? (x)
                  then 0
                  else - ( ( (maker maker) -(x,1) ), -4 )
                  in let times4 = proc (x) ((makemult makemult) x)
                     in (times4 3)")

(define times
  "let makemult = proc (y) proc (maker)
                  proc (x)
                  if zero? (x)
                  then 0
                  else - (((maker maker) -(x,1)), -(0,y))
                  in let times = proc (y)
                                 proc (x)
                                 (((makemult y) (makemult y)) x)
                                 in ((times 10) 20)")

(define factorial
  "let makemult = proc (y) proc (maker)
                  proc (x)
                  if zero? (x)
                  then 0
                  else - (((maker maker) -(x,1)), -(0,y))
                  in let times = proc (y)
                                 proc (x)
                                 (((makemult y) (makemult y)) x)
                                 in let prodmake = proc (maker)
                                                   proc (x)
                                                   if zero? (x)
                                                   then 1
                                                   else (( times (( maker maker) -(x,1))) x)
                                                   in let fact = proc (x) ((prodmake prodmake) x)
                                                       in (fact 5)")

(define sum
  "let maker = proc (maker)
               proc (x)
               if zero? (x)
               then 0
               else - (((maker maker) -(x,1)), -(0,x))
               in let sum = proc (x) ((maker maker) x)
                  in (sum 10)")

(define exp
  "let x = 3
      in let y = -(x,3)
           in if zero?(y) then -1 else y")

(define f1
  "let f = proc (x) proc (y) proc (z) -(-(x,y),z) in (f (f 1) 2)")


                
           
             
    
    

